!{\src2tex{textfont=tt}}
!!****f* ABINIT/xmpi_irecv
!! NAME
!!  xmpi_irecv
!!
!! FUNCTION
!!  This module contains functions that call MPI routine MPI_IRECV,
!!  to receive data on one processor sent by another,
!!  if we compile the code using the MPI CPP flags.
!!  xmpi_irecv is the generic function.
!!
!! COPYRIGHT
!!  Copyright (C) 2001-2022 ABINIT group
!!  This file is distributed under the terms of the
!!  GNU General Public License, see ~ABINIT/COPYING
!!  or http://www.gnu.org/copyleft/gpl.txt .
!!
!! TODO
!!
!! SOURCE
!!***

!!****f* ABINIT/xmpi_irecv_intv
!! NAME
!!  xmpi_irecv_intv
!!
!! FUNCTION
!!  Receives data from one processor sent by another.
!!  Target: single integer.
!!
!! INPUTS
!!  source :: rank of source process
!!  tag :: integer message tag
!!  comm :: MPI communicator
!!
!! OUTPUT
!!  ierr= exit status, a non-zero value meaning there is an error
!!
!! SIDE EFFECTS
!!  xval= buffer array
!!
!! NOTES
!!  status of MPI_IRECV is explicitly ignored
!!
!! PARENTS
!!
!! CHILDREN
!!      mpi_irecv
!!
!! SOURCE

subroutine xmpi_irecv_intv(xval,source,tag,comm,request,ierr)

!Arguments-------------------------
 integer ABI_ASYNC, intent(inout), target :: xval
 integer,intent(in) :: source,tag,comm
 integer,intent(out) :: ierr
 integer, intent(out) :: request

!Local variables-------------------
#if defined HAVE_MPI
 integer :: my_tag
 integer, pointer :: arr_xval(:)
 type(c_ptr) :: cptr
#endif

! *************************************************************************

 ierr=0
#if defined HAVE_MPI
 if (comm /= MPI_COMM_SELF .and. comm /= MPI_COMM_NULL) then
   my_tag=MOD(tag,xmpi_tag_ub)
   cptr=c_loc(xval) ; call c_f_pointer(cptr,arr_xval,[1])
   call MPI_IRECV(arr_xval,1,MPI_INTEGER,source,my_tag,comm,request,ierr)
   xmpi_count_requests = xmpi_count_requests + 1
 end if
#endif

 end subroutine xmpi_irecv_intv
!!***

!!****f* ABINIT/xmpi_irecv_int1d
!! NAME
!!  xmpi_irecv_int1d
!!
!! FUNCTION
!!  Sends data from one processor to another.
!!  Target: integer one-dimensional arrays.
!!
!! INPUTS
!!  dest :: rank of destination process
!!  tag :: integer message tag
!!  comm :: MPI communicator
!!
!! OUTPUT
!!  ierr= exit status, a non-zero value meaning there is an error
!!
!! SIDE EFFECTS
!!  xval= buffer array
!!
!! PARENTS
!!
!! CHILDREN
!!      mpi_irecv
!!
!! SOURCE

subroutine xmpi_irecv_int1d(xval,source,tag,comm,request,ierr)

!Arguments-------------------------
 integer ABI_ASYNC, intent(inout) :: xval(:)
 integer,intent(in) :: source,tag,comm
 integer, intent(out) :: request
 integer,intent(out) :: ierr
!Local variables-------------------

#if defined HAVE_MPI
  integer :: n1,my_tag
#endif

! *************************************************************************

 ierr=0
#if defined HAVE_MPI
 if (comm /= MPI_COMM_SELF .and. comm /= MPI_COMM_NULL) then
   my_tag=MOD(tag,xmpi_tag_ub)
   n1=size(xval)
   call MPI_IRECV(xval,n1,MPI_INTEGER,source,my_tag,comm,request,ierr)
   xmpi_count_requests = xmpi_count_requests + 1
 end if
#endif

 end subroutine xmpi_irecv_int1d
!!***

!!****f* ABINIT/xmpi_irecv_dp1d
!! NAME
!!  xmpi_irecv_dp1d
!!
!! FUNCTION
!!  Receives data from one proc sent by another.
!!  Target: double precision one-dimensional arrays.
!!
!! INPUTS
!!  source :: rank of source process
!!  tag :: integer message tag
!!  comm :: MPI communicator
!!
!! OUTPUT
!!  ierr= exit status, a non-zero value meaning there is an error
!!
!! SIDE EFFECTS
!!  xval= buffer array
!!
!! NOTES
!!  status of MPI_IRECV is explicitly ignored
!!
!! PARENTS
!!
!! CHILDREN
!!      mpi_irecv
!!
!! SOURCE

subroutine xmpi_irecv_dp1d(xval,source,tag,comm,request,ierr)

!Arguments-------------------------
 real(dp) ABI_ASYNC, intent(inout) :: xval(:)
 integer, intent(in) :: source,tag,comm
 integer, intent(out) :: ierr
 integer, intent(out) :: request

!Local variables-------------------
#if defined HAVE_MPI
 integer :: my_tag,n1
#endif

! *************************************************************************

 ierr=0
#if defined HAVE_MPI
 if (comm /= MPI_COMM_SELF .and. comm /= MPI_COMM_NULL) then
   n1=size(xval,dim=1)
   my_tag=MOD(tag,xmpi_tag_ub)
   call MPI_IRECV(xval,n1,MPI_DOUBLE_PRECISION,source,my_tag,comm,request,ierr)
   xmpi_count_requests = xmpi_count_requests + 1
 end if
#endif

end subroutine xmpi_irecv_dp1d
!!***

!!****f* ABINIT/xmpi_irecv_dp2d
!! NAME
!!  xmpi_irecv_dp2d
!!
!! FUNCTION
!!  Receives data from one proc sent by another.
!!  Target: double precision one-dimensional arrays.
!!
!! INPUTS
!!  source :: rank of source process
!!  tag :: integer message tag
!!  comm :: MPI communicator
!!
!! OUTPUT
!!  ierr= exit status, a non-zero value meaning there is an error
!!
!! SIDE EFFECTS
!!  xval= buffer array
!!
!! NOTES
!!  status of MPI_IRECV is explicitly ignored
!!
!! PARENTS
!!
!! CHILDREN
!!      mpi_irecv
!!
!! SOURCE

subroutine xmpi_irecv_dp2d(xval,source,tag,comm,request,ierr)

!Arguments-------------------------
 real(dp) ABI_ASYNC, intent(inout) :: xval(:,:)
 integer, intent(in) :: source,tag,comm
 integer, intent(out) :: ierr
 integer, intent(out) :: request

!Local variables-------------------
#if defined HAVE_MPI
 integer :: my_dt,my_op,my_tag,n1,n2
 integer(kind=int64) :: ntot
#endif

! *************************************************************************

 ierr=0
#if defined HAVE_MPI
 if (comm /= MPI_COMM_SELF .and. comm /= MPI_COMM_NULL) then
   n1=size(xval,dim=1)
   n2=size(xval,dim=2)
   my_tag=MOD(tag,xmpi_tag_ub)

   !This product of dimensions can be greater than a 32bit integer
   !We use a INT64 to store it. If it is too large, we switch to an
   !alternate routine because MPI<4 doesnt handle 64 bit counts.
   ntot=int(n1*n2,kind=int64)

   if (ntot<=xmpi_maxint32_64) then
     call MPI_IRECV(xval,n1*n2,MPI_DOUBLE_PRECISION,source,my_tag,comm,request,ierr)
   else
     call xmpi_largetype_create(ntot,MPI_DOUBLE_PRECISION,my_dt,my_op,MPI_OP_NULL)
     call MPI_IRECV(xval,1,my_dt,source,my_tag,comm,request,ierr)
     call xmpi_largetype_free(my_dt,my_op)
   end if

   xmpi_count_requests = xmpi_count_requests + 1
 end if
#endif

end subroutine xmpi_irecv_dp2d
!!***

!!****f* ABINIT/xmpi_irecv_dp3d
!! NAME
!!  xmpi_irecv_dp3d
!!
!! FUNCTION
!!  Receives data from one proc sent by another.
!!  Target: double precision one-dimensional arrays.
!!
!! INPUTS
!!  source :: rank of source process
!!  tag :: integer message tag
!!  comm :: MPI communicator
!!
!! OUTPUT
!!  ierr= exit status, a non-zero value meaning there is an error
!!
!! SIDE EFFECTS
!!  xval= buffer array
!!
!! NOTES
!!  status of MPI_IRECV is explicitly ignored
!!
!! PARENTS
!!
!! CHILDREN
!!      mpi_irecv
!!
!! SOURCE

subroutine xmpi_irecv_dp3d(xval,source,tag,comm,request,ierr)

!Arguments-------------------------
 real(dp) ABI_ASYNC, intent(inout) :: xval(:,:,:)
 integer, intent(in) :: source,tag,comm
 integer, intent(out) :: ierr
 integer, intent(out) :: request

!Local variables-------------------
#if defined HAVE_MPI
 integer :: my_dt,my_op,my_tag,n1,n2,n3
 integer(kind=int64) :: ntot
#endif

! *************************************************************************

 ierr=0
#if defined HAVE_MPI
 if (comm /= MPI_COMM_SELF .and. comm /= MPI_COMM_NULL) then
   n1=size(xval,dim=1)
   n2=size(xval,dim=2)
   n3=size(xval,dim=3)
   my_tag=MOD(tag,xmpi_tag_ub)

   !This product of dimensions can be greater than a 32bit integer
   !We use a INT64 to store it. If it is too large, we switch to an
   !alternate routine because MPI<4 doesnt handle 64 bit counts.
   ntot=int(n1*n2*n3,kind=int64)

   if (ntot<=xmpi_maxint32_64) then
     call MPI_IRECV(xval,n1*n2*n3,MPI_DOUBLE_PRECISION,source,my_tag,comm,request,ierr)
   else
     call xmpi_largetype_create(ntot,MPI_DOUBLE_PRECISION,my_dt,my_op,MPI_OP_NULL)
     call MPI_IRECV(xval,1,my_dt,source,my_tag,comm,request,ierr)
     call xmpi_largetype_free(my_dt,my_op)
   end if

   xmpi_count_requests = xmpi_count_requests + 1
 end if
#endif

end subroutine xmpi_irecv_dp3d
!!***

