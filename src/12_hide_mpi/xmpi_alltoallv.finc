!{\src2tex{textfont=tt}}
!!****f* ABINIT/xmpi_alltoallv_dp2d
!! NAME
!!  xmpi_alltoallv_dp2d
!!
!! FUNCTION
!!  This module contains functions calling the MPI routine ALLTOALLV
!!  xmpi_alltoallv is the generic function.
!!
!! COPYRIGHT
!!  Copyright (C) 2001-2019 ABINIT group (AR,XG)
!!  This file is distributed under the terms of the
!!  GNU General Public License, see ~ABINIT/COPYING
!!  or http://www.gnu.org/copyleft/gpl.txt .
!!
!! INPUTS
!!  xval= buffer array
!!  sendcnts= number of sent elements
!!  sdispls= postions of values sent by the processor
!!  rdispls= positions of values received by the processor
!!  recvcnts= number of received elements
!!  comm= MPI communicator
!!
!! OUTPUT
!!  ier= exit status, a non-zero value meaning there is an error
!!
!! SIDE EFFECTS
!!  recvbuf= received buffer
!!
!! PARENTS
!!
!! CHILDREN
!!      mpi_alltoallv
!!
!! SOURCE

subroutine xmpi_alltoallv_dp2d(xval,sendcnts,sdispls,recvbuf,recvcnts,rdispls,comm,ier)

!Arguments-------------------------
 real(dp), DEV_CONTARRD intent(in) :: xval(:,:)
 real(dp), DEV_CONTARRD intent(inout) :: recvbuf(:,:)
 integer , DEV_CONTARRD intent(in) :: sendcnts(:),sdispls(:),rdispls(:),recvcnts(:)
 integer ,intent(in) :: comm
 integer ,intent(out) :: ier

!Local variables-------------------
 integer :: sc,sds,sdr,sz1
 integer :: i
 
! *********************************************************************

 ier=0
#if defined HAVE_MPI
 if (comm /= MPI_COMM_SELF .and. comm /= MPI_COMM_NULL) then
!  allgather xval on all proc. in comm
   call MPI_ALLTOALLV(xval,sendcnts,sdispls,MPI_DOUBLE_PRECISION,recvbuf,&
&   recvcnts,rdispls,MPI_DOUBLE_PRECISION,comm,ier)
 else if (comm == MPI_COMM_SELF) then
#endif
   sz1=size(xval,1)
   sds=0;if (size(sdispls)>0) sds=sdispls(1)/sz1
   sdr=0;if (size(rdispls)>0) sdr=rdispls(1)/sz1
   sc=size(xval,2);if (size(sendcnts)>0) sc=sendcnts(1)/sz1
!$OMP parallel do
   do i=1,sc
     recvbuf(:,sdr+i)=xval(:,sds+i)
   end do

#if defined HAVE_MPI
 end if
#endif

end subroutine xmpi_alltoallv_dp2d
!!***

!!****f* ABINIT/xmpi_alltoallv_int2d
!! NAME
!!  xmpi_alltoallv_int2d
!!
!! FUNCTION
!!  Sends data from all to all processes.
!!  Target: two-dimensional integer arrays.
!!
!! SOURCE

subroutine xmpi_alltoallv_int2d(xval,sendcnts,sdispls,recvbuf,recvcnts,rdispls,comm,ier)

!Arguments-------------------------
 integer, DEV_CONTARRD intent(in) :: xval(:,:)
 integer, DEV_CONTARRD intent(inout) :: recvbuf(:,:)
 integer, DEV_CONTARRD intent(in) :: sendcnts(:),sdispls(:),rdispls(:),recvcnts(:)
 integer,intent(in) :: comm
 integer,intent(out) :: ier

!Local variables-------------------
 integer :: sc,sds,sdr,sz1

! *********************************************************************

 ier=0
#if defined HAVE_MPI
 if (comm /= MPI_COMM_SELF .and. comm /= MPI_COMM_NULL) then
   call MPI_ALLTOALLV(xval,sendcnts,sdispls,MPI_INTEGER,recvbuf,&
&   recvcnts,rdispls,MPI_INTEGER,comm,ier)
 else if (comm == MPI_COMM_SELF) then
#endif
   sz1=size(xval,1)
   sds=0;if (size(sdispls)>0) sds=sdispls(1)/sz1
   sdr=0;if (size(rdispls)>0) sdr=rdispls(1)/sz1
   sc=size(xval,2);if (size(sendcnts)>0) sc=sendcnts(1)/sz1
   recvbuf(:,sdr+1:sdr+sc)=xval(:,sds+1:sds+sc)
#if defined HAVE_MPI
 end if
#endif

end subroutine xmpi_alltoallv_int2d
!!***

!!****f* ABINIT/xmpi_alltoallv_dp1d
!! NAME
!!  xmpi_alltoallv_dp1d
!!
!! FUNCTION
!!  Sends data from all to all processes.
!!  Target: double precision one-dimensional arrays.
!!
!! SOURCE

subroutine xmpi_alltoallv_dp1d(xval,sendcnts,sdispls,recvbuf,recvcnts,rdispls,comm,ier)

!Arguments-------------------------
 real(dp), DEV_CONTARRD intent(in) :: xval(:)
 real(dp), DEV_CONTARRD intent(inout) :: recvbuf(:)
 integer, DEV_CONTARRD intent(in) :: sendcnts(:),sdispls(:),recvcnts(:)
 integer,intent(in) :: comm, rdispls
 integer,intent(out) :: ier

!Local variables-------------------
 integer :: sc,sds,sdr
 integer :: i
#if defined HAVE_MPI
 integer, allocatable :: rdispls_on(:)
#endif

! *********************************************************************

 ier=0
#if defined HAVE_MPI
 if (comm /= MPI_COMM_SELF .and. comm /= MPI_COMM_NULL) then
   ABI_STAT_ALLOCATE(rdispls_on,(size(sendcnts)), ier)
   if (ier/= 0) call xmpi_abort(msg='error allocating rdispls_on in xmpi_alltoallv')
   rdispls_on = 0
   call MPI_ALLTOALLV(xval,sendcnts,sdispls,MPI_DOUBLE_PRECISION,recvbuf,&
&   recvcnts,rdispls_on,MPI_DOUBLE_PRECISION,comm,ier)
   ABI_DEALLOCATE(rdispls_on)
 else if (comm == MPI_COMM_SELF) then
#endif
   sdr=rdispls;sds=0;if (size(sdispls)>0) sds=sdispls(1)
   sc=size(xval);if (size(sendcnts)>0) sc=sendcnts(1)
!$OMP parallel do
   do i = 1, sc
     recvbuf(i)=xval(sds+i)
   end do
#if defined HAVE_MPI
 end if
#endif

end subroutine xmpi_alltoallv_dp1d
!!***

!!****f* ABINIT/xmpi_alltoallv_dp1d2
!! NAME
!!  xmpi_alltoallv_dp1d2
!!
!! FUNCTION
!!  Sends data from all to all processes.
!!  Target: double precision one-dimensional arrays.
!!
!! SOURCE

subroutine xmpi_alltoallv_dp1d2(xval,sendcnts,sdispls,recvbuf,recvcnts,rdispls,comm,ier)

!Arguments-------------------------
 real(dp), DEV_CONTARRD intent(in) :: xval(:)
 real(dp), DEV_CONTARRD intent(inout) :: recvbuf(:)
 integer, DEV_CONTARRD intent(in) :: sendcnts(:),sdispls(:),recvcnts(:),rdispls(:)
 integer,intent(in) :: comm
 integer,intent(out) :: ier

!Local variables-------------------
 integer :: sc,sds,sdr

! *********************************************************************

 ier=0
#if defined HAVE_MPI
 if (comm /= MPI_COMM_SELF .and. comm /= MPI_COMM_NULL) then
   call MPI_ALLTOALLV(xval,sendcnts,sdispls,MPI_DOUBLE_PRECISION,recvbuf,&
&   recvcnts,rdispls,MPI_DOUBLE_PRECISION,comm,ier)
 else if (comm == MPI_COMM_SELF) then
#endif
   sds=0;if (size(sdispls)>0) sds=sdispls(1)
   sdr=0;if (size(rdispls)>0) sdr=rdispls(1)
   sc=size(xval);if (size(sendcnts)>0) sc=sendcnts(1)
   recvbuf(sdr+1:sdr+sc)=xval(sds+1:sds+sc)
#if defined HAVE_MPI
 end if
#endif

end subroutine xmpi_alltoallv_dp1d2
!!***
